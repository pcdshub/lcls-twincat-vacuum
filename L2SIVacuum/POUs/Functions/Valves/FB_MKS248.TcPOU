<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.18">
  <POU Name="FB_MKS248" Id="{dcc23c7e-d235-4f95-baec-6ff95237a2bd}" SpecialFunc="None">
    <Declaration><![CDATA[(* MKS248 valve using MKS1249 Drive Module *)
FUNCTION_BLOCK FB_MKS248
VAR_INPUT
    i_xExtIlkOK	:	BOOL; //External Interlock, SET to TRUE if not used
    i_rReqPos	:	REAL; //Requested position
END_VAR
VAR_OUTPUT
    {attribute 'pytmc' := 'pv:'}
    iq_stVFN	:	ST_VCN; //Needle valve structure
END_VAR
VAR CONSTANT
    rOpenVoltage	:	REAL := 9.8;
    rCloseVoltage	:	REAL := 0;
END_VAR
VAR
    // Requested voltage
    rReqVoltage: REAL := 0;
    (*IO*)
    q_iRawPosition AT%Q* :INT;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* MKS248 valve using MKS1249 Drive Module *)


// Interlocking
iq_stVFN.xIlkOK := i_xExtIlkOK;
(*Checking which Control mode is selected*)
IF iq_stVFN.xIlkOK THEN
    IF iq_stVFN.eValveControl = OpenValve THEN
        iq_stVFN.rReqPosition := iq_stVFN.rUpperLimit;(*Percentage*)// iq_stVCN.rUpperLimit;
    ELSIF iq_stVFN.eValveControl = CloseValve THEN
        iq_stVFN.rReqPosition := 0; (*Percentage*)
    ELSIF iq_stVFN.eValveControl = ManualControl THEN
        iq_stVFN.rReqPosition := LIMIT(0, iq_stVFN.rReqPosition, iq_stVFN.rUpperLimit);
    ELSIF iq_stVFN.eValveControl = PressureControl THEN
        iq_stVFN.rReqPosition := LIMIT(0, i_rReqPos, iq_stVFN.rUpperLimit);
    END_IF
ELSE
    iq_stVFN.rReqPosition := 0;
    iq_stVFN.eValveControl := CloseValve;
END_IF

// Requested Voltage calculation
rReqVoltage := iq_stVFN.rReqPosition * (rOpenVoltage-rCloseVoltage)/100 + rCloseVoltage;
rReqVoltage := LIMIT(rCloseVoltage, rReqVoltage, rOpenVoltage); //The requested voltage should remain within this range
//Raw position calc
iq_stVFN.q_iRawPosition := REAL_TO_INT( 32767/10 * rReqVoltage);
(*IO Mapping*)
ACT_IO();]]></ST>
    </Implementation>
    <Action Name="ACT_IO" Id="{69f74d47-c8bc-4fd5-815e-84e03ce47480}">
      <Implementation>
        <ST><![CDATA[(*outputs*)
q_iRawPosition := iq_stVFN.q_iRawPosition;]]></ST>
      </Implementation>
    </Action>
    <Method Name="M_SetControlMode" Id="{94c2fc45-3191-4410-8db4-8bf31f50a35d}">
      <Declaration><![CDATA[METHOD PUBLIC M_SetControlMode
VAR_INPUT
    i_eValveControlMode	:	E_VCN;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[iq_stVFN.eValveControl := i_eValveControlMode;]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_SetReqPosition" Id="{f3548ad6-1dd0-4cbe-94ae-ffaa7c334e0f}">
      <Declaration><![CDATA[METHOD PUBLIC M_SetReqPosition
VAR_INPUT
    i_fReqPosition : REAL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[iq_stVFN.rReqPosition := i_fReqPosition;]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>