<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_MKS355" Id="{38a3c8b0-dd29-4080-aa69-774394df5987}" SpecialFunc="None">
    <Declaration><![CDATA[(* This function is for the Hot Cathode MKS 355.

This function provides ILK and Set Point Protection for the hot Cathode.
*)

FUNCTION_BLOCK FB_MKS355 EXTENDS FB_GaugeBase
VAR_IN_OUT

END_VAR
VAR_INPUT
	i_iPRESS_R_test : INT;
    PG	:	ST_VG;
    tRecoverDelay:TIME:=T#600S; (*Delay Time after the first cycle to start the device. Default is 600S*)
END_VAR
VAR_OUTPUT
{attribute 'pytmc' := '
    pv:
    '}
    IG : ST_VG;
END_VAR
VAR
    rV : REAL;
    GaugeTurnOnTmr : TON;
    tStartupTimer:TON;
    iTermBits: UINT := 30518 ; // The terminal's maximum value in bits
    (*IOs to be linked*)
    /// Controls and I/Os
    i_iPRESS_R AT %I* :INT; // input Pressure // Link to analog Input
    q_xHV_DIS AT %Q* : BOOL; // Enable High Voltage when True // 'TcLinkTo' (EP2624) ^Output
    // only for EL and ES terminal
    i_xHV_ON AT %I*	: BOOL; //  True when High Voltage is on  // 'TcLinkTo' (EL1084) ^Input

    binit:BOOL:=TRUE;
END_VAR

VAR CONSTANT
    vBase : REAL := 1.0;
    vDisconnected : REAL:= 0.001; //shift from 0 to eliminate single noise effect
    vTorr : REAL := 10;
    vGaugeOff: REAL := 10;
    rMinPressure: REAL := 1E-10;
    cDefaultPressure : REAL := 0;
    rDeadband : REAL :=1.0;
END_VAR

VAR PERSISTENT
    bWasOn : BOOL;
    bAutoRecover:BOOL;
    bAutoRecoverWrite:BOOL;
END_VAR


]]></Declaration>
    <Implementation>
      <ST><![CDATA[(*Startup time*)
IF (bInit) THEN
    tStartupTimer.IN:=TRUE;
    bInit :=FALSE;
END_IF
tStartupTimer(PT:=T#3S);

(* 355 Logarithmic Output Conversion, factory default configuration
   EL3174, EL3174-xxxx iTermbits 30518, 10V
*)
If (iTermBits=0) THEN iTermBits := 30518;END_IF
rV := 10*INT_TO_REAL(IG.i_iPRESS_R)/iTermBits;

(* Set Guage State based on the Analog voltage*)

IF rV <= vDisconnected AND IG.i_xHV_ON and IG.q_xHV_DIS THEN
    IG.eState := ValidLo;
    IG.rPRESS := rMinPressure;
ELSIF rV <= vDisconnected AND NOT IG.i_xHV_ON AND IG.q_xHV_DIS THEN
    IG.eState := GaugeDisconnected;
    IG.rPRESS := cDefaultPressure;
ELSIF rV > vDisconnected AND rV < (vGaugeOff -rDeadband) AND IG.i_xHV_ON AND IG.q_xHV_DIS THEN
    IG.eState := Valid;
    IG.rPRESS := LREAL_TO_REAL(EXPT(10,((rV*vBase)- vTorr)));
ELSIF rV >= (vGaugeOff -rDeadband) THEN
    IG.eState := Off;
    IG.rPRESS := cDefaultPressure;
ELSE
    IG.eState := OoR;
    IG.rPRESS := cDefaultPressure;
END_IF



(* Ion Gauge Protection Functions *)
(* If the PG pressure is greater than the VG.PRO_SP then the gauge is disabled *)
(* If the PG pressure is less than the VG.PRO_SP then the gauge is enabled *)
(* This FB also implements some hysteresis so the gauge doesn't have rapid power cycling while near the turn on boundary *)


IF PG.rPRESS <= IG.rPRO_SP AND PG.xPRESS_OK THEN
    IG.q_xHV_DIS := IG.xHV_SW;
    IG.xILKOk := TRUE;
ELSIF IG.q_xHV_DIS THEN
    IF IG.rPRESS > (IG.rPRO_SP + IG.rHYS_PR)  OR PG.rPRESS > (IG.rPRO_SP + IG.rHYS_PR) THEN
        IG.q_xHV_DIS := FALSE;
        IG.xHV_SW := FALSE;
    END_IF
ELSE
    IG.q_xHV_DIS := FALSE;
    IG.xHV_SW := FALSE;
    IG.xILKOk := FALSE;
END_IF

(* Pressure gauge OK checks *)

GaugeTurnOnTmr(IN:=IG.q_xHV_DIS, PT:=T#10S, Q=>IG.xTurnOnTime);

//Backwards compatibility
IG.xPRESS_OK := (IG.eState = Valid) OR (IG.eState = ValidLo) OR IG.xBAKEOUT;

(* Setpoint evaluation *)
IG.xAT_VAC := (IG.xPRESS_OK AND (IG.rPRESS < IG.rVAC_SP)) OR IG.xBAKEOUT AND (IG.eState = Valid) ;

ACT_Logger();
(*Soft IO Linking*)
// check ethercat Diagnostics
IO();
(*Load or save the persistent variables*)
ACT_Persistent();
(*Run Auto Recovery*)
ACT_Recover();]]></ST>
    </Implementation>
    <Action Name="ACT_Logger" Id="{70f6be0b-a0e5-4c53-876a-650c1a146e71}">
      <Implementation>
        <ST><![CDATA[// ILK logger
IF (IG.xLog) THEN
    IF NOT IG.xILKOk AND IG.q_xHV_DIS THEN
            fbLogger(sMsg:='Lost external interlock while gauge was on.', eSevr:=TcEventSeverity.Critical);
    END_IF


    // Log Action
    tAction(CLK:=   IG.xHV_SW);
    IF tAction.Q THEN fbLogger(sMsg:='Ion gauge commanded to switch on', eSevr:=TcEventSeverity.Info); END_IF



    //STATE Logger
    tState(CLK:=  ePrevState <> IG.eState);
    IF ePrevState <> IG.eState THEN
          CASE IG.eState OF
            ValidHi:
                fbLogger(sMsg:='Gauge pressure valid high.', eSevr:=TcEventSeverity.Info);
            ValidLo:
                fbLogger(sMsg:='Gauge pressure valid low.', eSevr:=TcEventSeverity.Info);
            Valid:
                fbLogger(sMsg:='Gauge pressure valid.', eSevr:=TcEventSeverity.Info);
            GaugeDisconnected:
                fbLogger(sMsg:='Gauge Disconnected.', eSevr:=TcEventSeverity.Critical);
            PressInvalid:
                fbLogger(sMsg:='Gauge pressure invalid.', eSevr:=TcEventSeverity.Warning);
            OoR:
                fbLogger(sMsg:='Gauge pressure out of range.', eSevr:=TcEventSeverity.Warning);
            Starting:
                fbLogger(sMsg:='Gauge starting.', eSevr:=TcEventSeverity.Info);
          END_CASE
          ePrevState := IG.eState;
          stateTimer.IN := TRUE;
      END_IF
END_IF


]]></ST>
      </Implementation>
    </Action>
    <Action Name="ACT_Persistent" Id="{814dd36e-8ecd-4fbc-86bf-773954ba3627}">
      <Implementation>
        <ST><![CDATA[(*On first PLC pass, load the persistent value into the structrue variable*)
IF (SUPER^.bRestorePersistentData) THEN
    SUPER^.bRestorePersistentData	:= FALSE;
    IF ( rVAC_SP <> 0) THEN
        IG.rVAC_SP := rVAC_SP;
    END_IF;
    IF ( rPRO_SP <> 0) THEN
        IG.rPRO_SP := rPRO_SP;
    END_IF;
    IF ( rHYS_PR <> 0) THEN
        IG.rHYS_PR := rHYS_PR;
    END_IF;
    IF ( bAutoRecoverWrite) THEN
        IG.xAutoOn := bAutoRecover;
    END_IF;
END_IF
(*Check if a new value has been written in the structure variable copy it to the persistent variable*)
IF NOT (IG.rVAC_SP = rVAC_SP)  THEN
    rVAC_SP := IG.rVAC_SP;
END_IF;

IF NOT (IG.rPRO_SP = rPRO_SP) THEN
    rPRO_SP:= IG.rPRO_SP;
END_IF;

IF NOT (IG.rHYS_PR = rHYS_PR) THEN
    rHYS_PR:= IG.rHYS_PR;
END_IF;

IF NOT(bAutoRecoverWrite) OR NOT (bAutoRecover = IG.xAutoOn) THEN
    bAutoRecover := IG.xAutoOn;
    bAutoRecoverWrite := TRUE;
END_IF;
stateTimer(PT:=T#10S);
IF (stateTimer.Q) THEN
    bWasOn := (IG.eState >=Valid );
    stateTimer.IN := FALSE;
    fbWritePersistentData(NETID:='', PORT:=851, START:=TRUE, TMOUT:=T#1s );
END_IF;]]></ST>
      </Implementation>
    </Action>
    <Action Name="ACT_Recover" Id="{76decc4d-3ac7-48a2-94ce-8cf80b1f0678}">
      <Implementation>
        <ST><![CDATA[fbGetCurTaskIdx();
IF (TwinCAT_SystemInfoVarList._TaskInfo[fbGetCurTaskIdx.index].FirstCycle) THEN
    tRecover.IN := TRUE;
END_IF
tRecover(IN:= , PT:=tRecoverDelay);
IF NOT(bAutoRecoverWrite) OR (tRecover.Q AND bAutoRecover ) THEN
    This^.M_Recover();
    tRecover.IN := FALSE;
END_IF]]></ST>
      </Implementation>
    </Action>
    <Action Name="IO" Id="{7daa577d-1d6b-4516-8e97-e1ccf6f5ac48}">
      <Implementation>
        <ST><![CDATA[(*soft link inputs*)
IG.i_iPRESS_R:=	i_iPRESS_R;
IG.i_xHV_ON:= i_xHV_ON;
//IG.i_xDisc_Active:=i_xDisc_Active;
(*soft link outputs*)
q_xHV_DIS := IG.q_xHV_DIS;

IG.sPath := sPath;
IG.iAutoOnTimer:= TIME_TO_INT(tRecover.PT);]]></ST>
      </Implementation>
    </Action>
    <Method Name="M_HVE" Id="{155700a1-a1ed-477e-a403-3359b81935c6}">
      <Declaration><![CDATA[METHOD M_HVE : BOOL
VAR_INPUT
        enable:bool; // set to true to enable, false to disable;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[this^.IG.xHV_SW:= enable;]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Recover" Id="{60bf29c9-7bd7-43ed-8eff-3bb92750894a}">
      <Declaration><![CDATA[METHOD M_Recover : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[This^.M_HVE(This^.bWasOn);
M_Recover := bWasOn;
IF (bWasOn) THEN fbLogger(sMsg:='Auto Recovery Initiated.', eSevr:=TcEventSeverity.Info); END_IF;]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_SetBits" Id="{ca579050-89eb-49b8-8718-81e53a11da1a}">
      <Declaration><![CDATA[METHOD M_SetBits : BOOL
VAR_INPUT
    TermBits : UINT; // The terminal's maximum value in bits
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[This^.iTermBits := TermBits;
]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_MKS355">
      <LineId Id="3" Count="70" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_MKS355.ACT_Logger">
      <LineId Id="2" Count="37" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FB_MKS355.ACT_Persistent">
      <LineId Id="2" Count="37" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FB_MKS355.ACT_Recover">
      <LineId Id="2" Count="7" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FB_MKS355.IO">
      <LineId Id="2" Count="7" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FB_MKS355.M_HVE">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_MKS355.M_Recover">
      <LineId Id="3" Count="1" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_MKS355.M_SetBits">
      <LineId Id="3" Count="0" />
      <LineId Id="2" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>